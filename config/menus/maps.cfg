reset_map_ui = 0
selected_map = 0
selected_mutators = 0
selected_mode = -1
last_mode = -1
last_mutators = 0

map_index = 0
last_mode = -1 // already created previously
last_mutators = 0 // already created previously
map_number = -1
current_map = ""
map_pth = ""
maplist = ""
map_path = ""
map_extra = ""
map_image = ""
map_octa = 0
if (getalias mapfavs) [] [mapfavs = ""]
setpersist mapfavs 1
setcomplete mapfavs 1
if (getalias demofavs) [] [demofavs = ""]
setpersist demofavs 1
setcomplete demofavs 1
if (getalias gamefavs) [] [gamefavs = ""]
setpersist gamefavs 1
setcomplete gamefavs 1
reset_map_ui = 1
maps_count = 20
search_filter = 0

mapsmenuinit = [
    selected_mode = -1
    selected_mutators = 0
    map_index = 0
    last_mode = -1
    last_mutators = 0
    map_number = -1
    current_map = ""
    map_pth = ""
    maplist = ""
    map_path = ""
    map_extra = ""
    map_image = ""
    map_octa = 0
    reset_map_ui = 1
    nummodes = 0
    curmode = 0
    loop g (- $modeidxnum 1) [
        q = (+ $g 1)
        if (! (ismodelocked $q)) [ nummodes = (+ $nummodes 1); curmode = $q ]
    ]
    if (= $nummodes 1) [ selected_mode = $curmode ]
]

mapsmenuiter = [
    if (|| [!= $last_mode $selected_mode] [!= $last_mutators $selected_mutators]) [ reset_map_ui = 1 ]
    if (= $reset_map_ui 1) [
        maprot = 0
        maplist = ""
        map_path = ""
        if (= $selected_mode $modeidxdemo) [
            selected_mutators = 0
            demofavs = (sortlist $demofavs a b [<s $a $b])
            filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (? $q (< (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0)) [
                        append maplist $lcurmap
                        append map_path [demos/@lcurmap]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            append maplist "?"
            append map_path "?"
        ] [
            selected_mutators = (mutscheck $selected_mode $selected_mutators)
            append maplist "<random>"
            append map_path "<random>"
            mapfavs = (sortlist $mapfavs a b [<s $a $b])
            curlist = (sortlist (? (>= $selected_mode $modeidxplay) (getmaplist $selected_mode $selected_mutators (? (isonline) (listlen (listclients 1)) 0)) $allowmaps) a b [<s $a $b])
            curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $curlist [
                    if (< (listfind pcurmap $curprev [stringcmp $pcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            append maplist $lcurmap
                            append map_path [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            wantlist = 1
            looplist lcurmap $curprev [
                if $wantlist [
                    append maplist "~"
                    append map_path "~"
                    wantlist = 0
                ]
                append maplist $lcurmap
                append map_path [maps/@lcurmap]
            ]
            wantlist = 1
            filelist = (sortlist (listfiles maps mpz) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (< (listfind mcurmap $maplist [stringcmp $mcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "*"
                                append map_path "*"
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append map_path [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $map_octa 0] [> $hasoctapaks 0]) [
                wantlist = 1
                filelist = (sortlist (listfiles base ogz) a b [<s $a $b])
                loop q 2 [
                    looplist lcurmap $filelist [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "."
                                append map_path "."
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append map_path [base/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= 1 $search_filter) (> (stringlen $map_extra) 0)) [
                // apply the search filter and keep special tags $t
                t = "~*.?"
                maplist = (listfilter xcurmap $maplist [ (|| (> (stringstr $xcurmap $map_extra) -1) (> (stringstr $t $xcurmap) -1))])
                map_path = (listfilter xcurmap $map_path [ (|| (> (stringstr $xcurmap $map_extra) -1) (> (stringstr $t $xcurmap) -1))])
                // if there are no maps between two $t tags, remove the first tag
                // if no matching maps are available to vote, begin with a "-" tag
                if (> (stringstr $t (at $maplist 0)) -1) [
                    xcurmap = "-"
                    pcurmap = "-"
                ] [
                    xcurmap = ""
                    pcurmap = ""
                ]
                loop i (listlen $maplist) [
                    if (|| (= (stringstr $t (at $maplist $i)) -1) (= (stringstr $t (at $maplist (+ 1 $i))) -1)) [
                        append xcurmap (at $maplist $i)
                        append pcurmap (at $map_path $i)
                    ]
                ]
                maplist = $xcurmap
                map_path = $pcurmap
            ]
            append maplist "?"
            append map_path "?"
            map_number = (listfind curmap $maplist [
                || [=s $curmap $current_map] [=s [maps/@curmap] $current_map] [
                    && [> $hasoctapaks 0] [> $map_octa 0] [=s [base/@curmap] $current_map]
                ]
            ])
        ]
        map_count = (listlen $maplist)
        if (! $maprot) [ maprot = $map_count ]
        last_mode = $selected_mode
        last_mutators = $selected_mutators
        map_index = 0
        reset_map_ui = 0
    ]
    if (|| [< $map_number 0] [>= $map_number $map_count]) [
        map_number = (? (= $selected_mode $modeidxdemo) -1 0)
        current_map = ""
        map_pth = ""
        mapdmo = ""
        map_image = ""
    ] [
    cases (at $maplist $map_number) "?" [
        current_map = $map_extra
        if (= $selected_mode $modeidxdemo) [
            map_pth = [demos/@current_map]
            mapdmo = (demoscan (format "%1.dmo" $map_pth))
            map_image = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            map_pth = $map_extra
            map_image = maps/@map_extra
        ]
    ] "<random>" [
        current_map = "<random>"
        map_pth = "<random>"
        map_image = "question"
    ] () [
        current_map = (at $maplist $map_number)
        map_pth = (at $map_path $map_number)
        if (= $selected_mode $modeidxdemo) [
            mapdmo = (demoscan (format "%1.dmo" $map_pth))
            map_image = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            map_image = (at $map_path $map_number)
        ]
    ] ]
]

mutsvar = [
    local g m s t
    ui_stay_open [ ui_list [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (< $g 0) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            if (|| (& (mutsimplied $g $m) $arg4) (& $mutslockforce $arg4)) [
                ui_button $arg1 [ implied = @arg4 ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 "checkboxon" $ui_color_checkbox_two
            ] [
                if (ismodelocked $g (| $m $arg4) $arg4 $s) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
                    t = (& $m $arg4)
                    ui_button $arg1 [
                        mutate = @arg4
                        if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                            @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                        ]
                    ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $t "checkboxon" "") $ui_color_checkbox
                ]
            ]
        ]
    ] ]
]

modevar = [
    local g m s
    ui_stay_open [ ui_list [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            ui_button $arg1 [@arg2 = @arg4] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (= $$arg2 $arg4) "checkboxon" "") $ui_color_checkbox
        ]
    ] ]
]

mapsexec = [
    sleep 1 [
        if (isconnected) [ show_ui maps 2 ]
        start @arg1 @arg2 @arg3
    ]
]

mapsmenu = [
    ui_list [
        ui_strut 70 1
        ui_list [
            ui_list [
                ui_list [
                    ui_strut 3 1
                    ui_list [
                        ui_strut 38 1
                        if (< $last_mode 0) [
                            ui_font "emphasis" [ ui_text "Game Select" ]
                            ui_text "Please select a mode and map to continue"
                        ] [
                            gname = (gamename $selected_mode $selected_mutators 0 32)
                            if (&& (stringlen $current_map) (= $selected_mode $modeidxdemo)) [
                                dinfo = (demoscan (format "%1.dmo" $map_pth))
                                dmode = (demoinfo $dinfo 2)
                                dmuts = (demoinfo $dinfo 3)
                                dname = (gamename $dmode $dmuts 0 26)
                                gname = (concat $dname "(demo)")
                            ]
                            ui_font "emphasis" [ ui_text $gname ]
                            ui_list [
                                if (stringlen $current_map) [
                                    if (= $selected_mode $modeidxdemo) [
                                        dinfo = (demoscan (format "%1.dmo" $map_pth))
                                        dmapname = (demoinfo $dinfo 1)
                                        ui_text " ^farecorded on "
                                        ui_text $dmapname
                                    ] [
                                        ui_text " ^faselected on "
                                        ui_text $current_map
                                    ]
                                ] [ ui_text "Please select a map to continue" ]
                            ]
                        ]
                        ui_strut 0.125
                        ui_list [
                            ui_spring
                            ui_stay_open [ ui_button "^fvPick Random" [
                                pickrandom = 1
                                nummodes = 0
                                cntmodes = (- $modeidxnum $modeidxplay)
                                selected_mode = (+ (rnd $cntmodes) $modeidxplay)
                                while [&& [ismodelocked $selected_mode] [<= $nummodes $cntmodes]] [
                                    selected_mode = (+ $selected_mode 1)
                                    if (>= $selected_mode $modeidxnum) [ selected_mode = $modeidxplay ]
                                    nummodes = (+ $nummodes 1)
                                ]
                                selected_mutators = (rnd (+ $mutsbitall 1))
                                loop g $mutsidxnum [
                                    q = (<< 1 $g)
                                    if (ismodelocked $selected_mode $q) [ selected_mutators = (&~ $selected_mutators $q) ]
                                ]
                            ] ]
                            ui_spring
                            if (isconnected) [
                                ui_stay_open [
                                    ui_button "^foCopy Current" [
                                        copycurrent = 1
                                        selected_mode = (gamemode)
                                        if (< (mutators) 0) [ selected_mutators = 0 ] [ selected_mutators = (mutscheck $selected_mode (mutators)) ]
                                    ]
                                ]
                                ui_spring
                            ]
                            ui_stay_open [ ui_button "^fyReset Selection" [
                                resetselection = 1
                                selected_mode = -1
                                selected_mutators = 0
                                map_number = -1
                                current_map = ""
                                map_pth = ""
                                map_image = ""
                                mapdmo = ""
                            ] ]
                            ui_spring
                        ]
                        ui_strut 0.125
                    ]
                ]
                ui_list [
                    ui_list [
                        ui_font "emphasis" [ ui_text "Mode" ]
                        ui_strut 0.25
                        loop z $modeidxnum [
                            modevar (at $modename $z) selected_mode selected_mutators $z current_map
                        ]
                    ]
                    ui_strut 3
                    ui_list [
                        ui_stay_open [
                            if (stringlen $current_map) [ 
                                ui_image $map_image [mapsexec @current_map @selected_mode @selected_mutators ] 5 1 "textures/emblem" [
                                    fav = (format "%1.%2.%3" @current_map @selected_mode @selected_mutators)
                                    gamefavs = (concat $fav (listdel $gamefavs $fav)) 
                                    conout "Added to favorites list"
                                ]
                            ] [
                                ui_no_skin_fx [ui_image "textures/emblem" [ conout 1 "You haven't selected a map" ] 5 1 ]
                            ]
                        ]
                    ]
                ]
                ui_font "emphasis" [ ui_text "Mutators" ]
                ui_strut 0.25
                cnt = (- $mutsidxnum $mutsidxgsn)
                ui_loop_split n 3 $cnt [
                    mutsvar (at $mutsname $n) selected_mode selected_mutators (<< 1 $n) current_map
                ] [ ui_strut 3 ]
                if (> (stringlen (gspmutname $selected_mode 0)) 0) [
                    ui_strut 1
                    ui_font "emphasis" [ ui_text "mode specific mutators" ]
                    ui_strut 0.25
                    ui_loop_split n 3 $mutsidxgsn [
                        mutator = (gspmutname $selected_mode $n)
                        if (stringlen $mutator) [
                            mutsvar $mutator selected_mode selected_mutators (<< 1 (+ $mutsidxgsp $n)) current_map
                        ]
                    ] [ ui_strut 3 ]
                ]
            ]
            ui_strut 1
            ui_list [
                ui_list [
                    if $selected_mode [ ui_text "Available Maps:" ] [ ui_text "Available Demos:" ]

                    if (> $hasoctapaks 0) [
                        ui_spring
                        ui_stay_open [
                            ui_button "Show Sauer Maps" [
                                map_octa = (! $map_octa)
                                reset_map_ui = 1
                            ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (> $map_octa 0) "checkboxon" "") $ui_color_checkbox
                        ]
                        ui_spring 1
                        ui_text "Fav"
                        ui_strut 3
                    ]
                ]
                ui_strut 0.25
                ui_list [
                    number_of_maps = (- (listlen $maplist) 1)
                    ui_list [
                        ui_list [
                            ui_strut $maps_count 1
                            map_index = (min (max 0 (- $number_of_maps $maps_count)) $map_index) //safeguard
                            map_number = (min $map_number $number_of_maps)
                            ui_list [
                                ui_strut 37 1
                                break = 0
                                loopwhile i $maps_count [= $break 0] [
                                    q = (+ $map_index $i)
                                    curmap = (at $maplist $q)
                                    cases $curmap "*" [
                                        ui_text "Maps not in the rotation:"
                                    ] "~" [
                                        ui_text "Maps played recently:"
                                    ] "." [
                                        ui_text "Maps from sauerbraten:"
                                    ] "-" [
                                        ui_text "^faNone for this search filter" "textures/info"
                                    ] "?" [
                                        break = 1
                                    ] () [
                                        ui_list [
                                            ui_radiobutton (stringreplace $curmap $map_extra (format "^fs^fy%1^fS" $map_extra)) map_number $q
                                            ui_spring 1
                                            ui_stay_open [
                                                hasmap = (>= (indexof (? (= $selected_mode $modeidxdemo) $demofavs $mapfavs) $curmap) 0)
                                                ui_button "" [
                                                    if (= $selected_mode $modeidxdemo) [
                                                        demofavs = (? @@hasmap (listdel $demofavs @@curmap) (concat (listdel $demofavs @@curmap) @@curmap))
                                                    ] [
                                                        mapfavs = (? @@hasmap (listdel $mapfavs @@curmap) (concat (listdel $mapfavs @@curmap) @@curmap))
                                                    ]
                                                    reset_map_ui = 1
                                                ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $hasmap "checkboxon" "") $ui_color_checkbox
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        ui_list [
                            ui_text "Custom map or "
                            ui_checkbox "Search filter" search_filter 1 0 [reset_map_ui = 1]
                        ]
                        ui_list [
                            map_extra_value = $map_extra
                            map_extra_number = $number_of_maps
                            ui_radiobutton "" map_number $map_extra_number
                            ui_textfield map_extra_value 34 [map_extra = $map_extra_value; map_number = $map_extra_number; reset_map_ui = $search_filter] -1 0 "" 0 "^fd<enter map name>" $search_filter
                        ]
                    ]
                    ui_slider map_index 0 (max (- $number_of_maps $maps_count) 0) [] 1 1
                ]
            ]
        ]
        ui_strut 1
        ui_list [
            if (! (isonline)) [
                ui_text "Server type:"; ui_strut 0.5
                ui_radiobutton "Offline" servertype 0; ui_strut 0.5
                ui_radiobutton "Private" servertype 1; ui_strut 0.5
                ui_radiobutton "Public" servertype 2
            ]
            ui_spring 1
            ui_font "default" [
                if (>= $last_mode 0) [
                    ui_stay_open [
                        if $selected_mode [
                            if (getclientcount) [
                                // other players are present
                                if (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) [
                                    ui_button "^fzwyForce new game (veto)" [ mapsexec $current_map $selected_mode $selected_mutators ]
                                ] [
                                    ui_button "^fgSubmit this vote" [ mapsexec $current_map $selected_mode $selected_mutators ]
                                ]
                            ] [
                                //either playing offline or without other players
                                ui_button "^fgStart a new game" [ mapsexec $current_map $selected_mode $selected_mutators ]
                            ]
                            ui_strut 2
                            ui_button "Add to favourites" [
                                fav = (format "%1.%2.%3" @current_map @selected_mode @selected_mutators)
                                gamefavs = (concat $fav (listdel $gamefavs $fav)) 
                            ] [] "" 0x0080ff
                        ] [
                            ui_button "^fgstart demo playback" [ mapsexec $current_map $selected_mode $selected_mutators ]
                        ]
                        ui_strut 14
                    ]
                ] [
                    ui_text "^fy.. pick a mode to continue .."
                    ui_strut 10
                ]
            ]
        ]
        if (&& (getclientpriv $getclientnum $masterlock) (getclientcount)) [
            ui_strut 0.2
            ui_stay_open [ ui_right [
                ui_button (concat "master mode:" (getmastermode 1)) [mastermode (! (getmastermode))]
                ui_strut 13.5
            ] ]
        ]
        ui_visible [
            cases (at $guirolloveraction 0) "selected_mode" [
                ui_tooltip (modedesc (at $guirolloveraction 2) $selected_mutators 3)
            ] "mutate" [
                ui_tooltip (mutsdesc $selected_mode (at $guirolloveraction 2) 3)
            ] "implied" [
                ui_tooltip "this is forced on by the current configuration"
            ] "disabled" [
                ui_tooltip "this is disabled by the current configuration"
            ]
            ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 1"))
        ]
    ]
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]



votemenu = [
    ui_big_macro vote 8 74 3.2 [1] (getvote) [
        voteplayers = (getvote $i 0)
        votemode = (getvote $i 1)
        votemuts = (getvote $i 2)
        votemap = (getvote $i 3)
        voteself = 0
        loop j $voteplayers [ if (= (getclientnum) (getvote $i 0 $j)) [ voteself = 1 ] ]
    ] [1] [
        ui_button "^fwThere are no votes currently pending, ^fgsubmit ^fwone yourself" [show_ui maps 1] [] "chat"
    ] [
        ui_list [
            ui_checkbox "^fcdynamic sort" sortvotes; ui_strut 1.5
            ui_checkbox "^focleanup list" cleanvotes; ui_strut 1.5
            ui_button "^fyclear vote" clearvote
            ui_spring
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwgame%2 suggested" $votenum (? (!= $votenum 1) "s"))
                    ui_strut 1
                ]
            ]
        ]
        ui_strut 0.25
    ] [
        ui_merge 72 [
            ui_center [
                ui_strut 4 1
                votecolour = (? $voteself "^fy" "^fw")
                ui_center [ui_font "default" [ui_button (concatword $votecolour $voteplayers)]]
                ui_center [ui_font "little" [ui_button (format "%1vote%2" $votecolour (? (!= $voteplayers 1) "s"))]]
            ]
            voteimage = "textures/emblem"
            votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                && [> $map_octa 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
            ]])
            if (> $votepath -1) [ voteimage = (at $map_path $votepath) ]
            ui_strut 1
            ui_image $voteimage "" 1.5 1 "textures/emblem"
            ui_strut 1
            ui_center [
                gname = (gamename $votemode $votemuts 0 32)
                ui_button (format "^fy%1 ^favoted for on ^fo%2" $gname $votemap)
                ui_list [
                    if (> $voteplayers 0) [
                        ui_button "^fwby "
                        ui_font "little" [
                            pname = ""
                            pmore = 0
                            loop j $voteplayers [
                                if (|| $pmore (> (ui_text_width $pname) 1220)) [ pmore = (+ $pmore 1) ] [
                                    append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                ]
                            ]
                            ui_button (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                        ]
                    ] [ ui_button "^fano current votes" ]
                ]
            ]
        ] [
            if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]
        ] [
            selected_mode = @votemode; selected_mutators = @votemuts; current_map = @votemap; show_ui maps 1
	]
    ] [
        ui_visible [ ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 2")) ]
    ]
]

favindex = 0
favnum = 0


favsmenu = [
    ui_big_macro fav 8 74 3.2 [1] (listlen $gamefavs) [
        favs = (at $gamefavs $i)
        favl = (stringreplace $favs "." " ")
        votemap = (at $favl 0)
        votemode = (at $favl 1)
        votemuts = (at $favl 2) 
        votebad = (ismodelocked $votemode $votemuts $votemuts $votemap)
        if (= -1 (stringstr (getmaplist $votemode $votemuts) $votemap)) [votebad = 1]
    ] [1] [
        ui_button "^fwuse the maps menu to save your favourite game modes." [show_ui maps 1] [] "info"
    ] [
        ui_text (format "Click to %1 a game, right click to copy your selection to the first tab." (? $getclientcount "vote" "start"))
        ui_strut 0.25
    ] [
        ui_strut 0.2
        ui_list [
            ui_body [
                ui_list [
                    voteimage = "textures/question"
                    votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                        && [> $map_octa 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
                    ]])
                    if (> $votepath -1) [ voteimage = (at $map_path $votepath) ]
                    ui_strut 1
                    ui_image $voteimage [] 1.5 1 
                    ui_strut 0.5
                    ui_center [
                        gname = (gamename $votemode $votemuts 0 60)
                        if $votebad [
                            ui_text (format " ^fd%1 on %2" $gname $votemap)
                            ui_text " ^frthis game is currently not allowed"
                        ] [
                            ui_button (format " ^fy%1 ^faon ^fo%2" $gname $votemap)
                            ui_list [
                                looplist i (modetexlist $votemode $votemuts 1) [ui_image $i [] 0.75]
                            ]
                        ]
                    ]
                ]
            ] [mapsexec @favl] [
		selected_mode = @votemode; selected_mutators = @votemuts; current_map = @votemap; show_ui maps 1
            ]
            ui_spring
            ui_center [ui_image $warningtex [gamefavs = (listdel $gamefavs @favs)] 0.8 1 [] "" 0xff3333]
            ui_center [ui_image $arrowtex [gamefavs = (concat @favs (listdel $gamefavs @favs))] 0.8 1]
            ui_center [ui_image $arrowdowntex [gamefavs = (concat (listdel $gamefavs @favs) @favs)] 0.8 1]
        ]
    ] [
        //ui_visible [ ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 3")) ]
    ]
]


new_ui maps [
    ui_header "Maps"
    octapaks [
        mapsmenuiter
        ui_list [ mapsmenu ]
        if (|| (getclientcount) (getvote)) [
            votesum = 0
            loop j $votenum [ votesum = (+ $votesum (getvote $j 0)) ]
            ui_tab (format "Votes (%1)" $votesum)
            ui_list [ votemenu ]
        ]
        ui_tab (format "Favourites (%1)" $favnum)
        ui_list [favsmenu]
    ]
] [
    if (= $guipasses 0) [
        octapaks [ mapsmenuinit; votemenuinit ]
    ]
]
